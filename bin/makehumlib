#!/usr/bin/perl
#
# Programmer:    Craig Stuart Sapp <craig.stanford.edu>
# Creation Date: Mon Aug 10 00:06:00 PDT 2015
# Last Modified: Sat Aug  6 11:26:12 CEST 2016
# Filename:      /bin/makehumlib
# Syntax:        perl 5
# vim:           ts=3
#
# Description:   Merge all library files into humlib.h and humlib.cpp.
#

use strict;

my $srcdir = "src";
my $incdir = "include";
my $license = getLicense("LICENSE.txt");

createIncludeFile($incdir, "humlib.h");
createSourceFile($srcdir, "humlib.cpp");

exit(0);



##############################
##
## createIncludeFile --
##

sub createIncludeFile {
	my ($basedir, $target) = @_;
	open (IFILE, ">$basedir/$target") or die;


	my $contents = "";
	# my @files = getFiles($basedir);
	my @files = (
		"HumHash.h",
		"HumNum.h",
		"HumRegex.h",
		"HumAddress.h",
		"HumInstrument.h",
		"HumdrumLine.h",
		"HumdrumToken.h",
		"HumdrumFileBase.h",
		"HumdrumFileStructure.h",
		"HumdrumFileContent.h",
		"HumdrumFile.h",
		"NoteCell.h",
		"NoteGrid.h",
		"Convert.h"
	);

	foreach my $file (@files) {
		$contents .= getMergeContents("$basedir/$file");
	}

	my $options = getMergeContents("$basedir/Options.h");
	$contents .= $options;

	# HumTool depends on Options class:
	$contents .= getMergeContents("$basedir/HumTool.h");

	# HumdrumFileStream depends on Options class:
	$contents .= getMergeContents("$basedir/HumdrumFileStream.h");

	my @tools = glob "$basedir/tool-*.h";

	foreach my $tool (@tools) {
		$tool =~ s/^include\///;
		$contents .= getMergeContents("$basedir/$tool");
	}

	my $date = `date`;
	chomp $date;

   print IFILE <<"EOT";
//
// Programmer:    Craig Stuart Sapp <craig\@ccrma.stanford.edu>
// Creation Date: Sat Aug  8 12:24:49 PDT 2015
// Last Modified: $date
// Filename:      humlib.h
// URL:           https://github.com/craigsapp/humlib/blob/master/include/humlib.h
// Syntax:        C++11
// vim:           ts=3
//
// Description:   Include file for humlib library.
//
/*
$license
*/

#ifndef _HUMLIB_H_INCLUDED
#define _HUMLIB_H_INCLUDED

#include <stdarg.h>
#include <string.h>

#include <algorithm>
#include <cctype>
#include <cmath>
#include <fstream>
#include <functional>
#include <iostream>
#include <locale>
#include <map>
#include <regex>
#include <set>
#include <sstream>
#include <string>
#include <utility>
#include <vector>

using std::cerr;
using std::cin;
using std::cout;
using std::endl;
using std::ends;
using std::ifstream;
using std::invalid_argument;
using std::istream;
using std::map;
using std::ostream;
using std::pair;
using std::regex;
using std::set;
using std::string;
using std::stringstream;
using std::to_string;
using std::vector;

#define USING_URI 1
#ifdef USING_URI
	#include <sys/types.h>   /* socket, connect */
	#include <sys/socket.h>  /* socket, connect */
	#include <netinet/in.h>  /* htons           */
	#include <netdb.h>       /* gethostbyname   */
	#include <unistd.h>      /* read, write     */
	#include <string.h>      /* memcpy          */
   #include <sstream>
#endif

namespace hum {

class Convert;
class HumNum;
class HumAddress;
class HumdrumToken;
typedef HumdrumToken* HTp;
class HumdrumLine;
class HumdrumFileBase;
class HumdrumFileStructure;
class HumdrumFileContent;
class HumdrumFile;

EOT

	print IFILE <<"EOT";
$contents
} // end of namespace hum


#endif /* _HUMLIB_H_INCLUDED */



EOT

	close IFILE;
}



##############################
##
## createSourceFile --
##

sub createSourceFile {
	my ($basedir, $target) = @_;
	open (SFILE, ">$basedir/$target") or die;

	my $date = `date`;
	chomp $date;

   print SFILE <<"EOT";
//
// Programmer:    Craig Stuart Sapp <craig\@ccrma.stanford.edu>
// Creation Date: Sat Aug  8 12:24:49 PDT 2015
// Last Modified: $date
// Filename:      /include/humlib.cpp
// URL:           https://github.com/craigsapp/humlib/blob/master/src/humlib.cpp
// Syntax:        C++11
// vim:           ts=3
//
// Description:   Source file for humlib library.
//
/*
$license
*/

#include "humlib.h"

namespace hum {

EOT

	my @files = getFiles($basedir);
	foreach my $file (@files) {
		print SFILE getMergeContents("$basedir/$file");
	}

print SFILE "\n} // end namespace hum\n";

	close SFILE;
}



##############################
##
## getMergeContents -- return the contents of a file between lines containing
## "START_MERGE" and "END_MERGE".
##

sub getMergeContents {
	my ($file) = @_;
	open (MFILE, "$file") || die "Cannot read $file\n";
	my @contents = <MFILE>;
	close MFILE;
	my $output = "";
	my $active = 0;
	foreach my $line (@contents) {
		if ($active == 0) {
			if ($line =~ /START_MERGE/) {
				$active = 1;
				next;
			}
		}
		if ($active && ($line =~ /END_MERGE/)) {
			$active = 0;
			next;
		}
		next if $active == 0;
		$output .= $line;
	}
	return $output;
}



##############################
##
## getFiles -- Get a list of a files in a directory.
## 

sub getFiles {
	my ($dir) = @_;
	opendir(DIR, $dir) or die;
	my @output;
	my @output2;
	while (my $file = readdir(DIR)) {
		next if $file =~ /^\./;
		next if -d "$dir/$file";
		next if !-r "$dir/$file";
		if ($file =~ /Convert/) {
			# classes which have a dependeny on other classes:
			$output2[@output2] = $file;
		} else {
			$output[@output] = $file;
		}
	}
	@output = sort @output;
	push(@output, @output2);
	return @output;
}



##############################
##
## getLicense -- 
## 

sub getLicense {
	my ($file) = @_;
	open (FILE, $file) or die "Cannot open $file\n";
	my @contents = <FILE>;
	close FILE;
	return join("", @contents);
}



